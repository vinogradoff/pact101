plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id "au.com.dius.pact" version "3.6.5"
}

apply plugin: 'io.spring.dependency-management'

ext['junit-jupiter.version'] = '5.4.2'

bootJar {
    archiveBaseName = 'newsportal-service'
    archiveVersion =  '1.0.0'
}


repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation 'io.rest-assured:rest-assured:3.3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    testImplementation "org.assertj:assertj-core:3.12.2"
    testImplementation "au.com.dius:pact-jvm-consumer-junit5_2.12:3.6.2"
    testImplementation "org.mockito:mockito-core:2.21.0"
    testImplementation "org.mockito:mockito-junit-jupiter:2.23.4"

    // Lambda DSL
    testImplementation "au.com.dius:pact-jvm-consumer-java8_2.12:3.6.2"
}

test {
    useJUnitPlatform()

    // don't merge with existing pact file, always overwrite.
    // this should work starting with 3.6.5
    systemProperty "pact.writer.overwrite",true
}

project.version="0.1.0"

def getGitHashForLastCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


pact {

    publish {
        pactDirectory = 'build/pact-files' // defaults to $buildDir/pacts
        pactBrokerUrl = 'https://comaqa.pact.dius.com.au/'
        pactBrokerUsername = pactBrokerUser
        pactBrokerPassword = pactBrokerPwd
        version="${project.version}-${getGitHashForLastCommit()}"

    }

}
